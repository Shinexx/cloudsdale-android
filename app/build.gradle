buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.4.+'
    }
}

apply plugin: 'android'
apply plugin: 'idea'

configurations {
    apt
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.koushikdutta.ion:ion:1.0.8'
    compile ('de.keyboardsurfer.android.widget:crouton:1.8.1') {
        exclude group: 'com.google.android', module: 'support-v4'
    }
    compile 'com.googlecode.android-query:android-query:0.25.9'
    compile 'com.android.support:support-v4:13.0.0'
    apt 'org.androidannotations:androidannotations:3.0-SNAPSHOT'
    compile 'org.androidannotations:androidannotations-api:3.0-SNAPSHOT'
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    sourceSets {
        instrumentTest.setRoot('test')
    }
}

android.applicationVariants.each { variant ->
    aptOutput = file("${project.buildDir}/source/apt_generated/${variant.dirName}")
    variant.javaCompile.doFirst {
        aptOutput.mkdirs()
        variant.javaCompile.options.compilerArgs += [
                '-processorpath', configurations.apt.getAsPath(),
                '-AandroidManifestFile=' + variant.processResources.manifestFile,
                '-s', aptOutput
        ]
    }
}
